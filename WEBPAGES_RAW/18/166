model
 {
 for(i in 1:n1){low[i] ~ dlnorm(mu1, tau)}
 for(i in 1:n2){normal[i] ~ dlnorm(mu2, tau)}
 for(i in 1:n3){high[i] ~ dlnorm(mu3, tau)}
 mu1 ~ dnorm(4.87, 347.12)
 mu2 ~ dnorm(5.39, 357.42)
 mu3 ~ dnorm(6.40, 166.67)
 tau ~ dgamma(0.001,0.001)
 lowf ~ dlnorm(mu1, tau)
 normalf ~ dlnorm(mu2, tau)
 highf ~ dlnorm(mu3, tau)
 P <- step(mu3-mu2)*step(mu2-mu1)*step(mu3-mu1) 
 diff21 <- mu2-mu1
 diff31 <- mu3-mu1
 diff32 <- mu3-mu2
 prob21 <- step(diff21)
 prob31 <- step(diff31)
 prob32 <- step(diff32)
 med1 <- exp(mu1)
 med2 <- exp(mu2)
 med3 <- exp(mu3)
 relmed21 <- med2/med1
 relmed31 <- med3/med1
 relmed32 <- med3/med2
 MAD <- (abs(diff21)+abs(diff31)+abs(diff32))/3
 prob <- step(MAD-0.75)
 for(i in 1:11){
   aL[i] <- piL*sqrt(tau/(2*3.14159))*(1/x[i])*exp(-(tau/2)*(log(x[i])-mu1)*(log(x[i]) - mu1))
   bL[i] <-  piN*sqrt(tau/(2*3.14159))*(1/x[i])*exp(-(tau/2)*(log(x[i])-mu2)*(log(x[i])-mu2))
   cL[i] <-  piH*sqrt(tau/(2*3.14159))*(1/x[i])*exp(-(tau/2)*(log(x[i])-mu3)*(log(x[i])-mu3))
   pL[i] <- aL[i]/(aL[i] + bL[i] + cL[i])
   pN[i] <- bL[i]/(aL[i] + bL[i] + cL[i]) 
   pH[i] <- cL[i]/(aL[i] + bL[i] + cL[i]) 
 }
}